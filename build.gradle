import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

group 'com.github.james9909'
version '0.1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Disable the regular jar task, since we only want the shadow jar
jar.enabled = false
tasks.build.dependsOn tasks.shadowJar

shadowJar {
    minimize()
    archiveName = "${baseName}-${version}.${extension}"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "http://maven.sk89q.com/repo/" }
    maven { url = "https://dl.bintray.com/michaelbull/maven" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "com.michael-bull.kotlin-result:kotlin-result:1.1.0"
    compile "org.xerial:sqlite-jdbc:3.7.2"
    compileOnly "com.sk89q.worldedit:worldedit-bukkit:7.0.0-SNAPSHOT"
    compileOnly "org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    testCompile "org.junit.jupiter:junit-jupiter:5.4.2"
    testCompile "com.github.seeseemelk:MockBukkit-v1.13:0.2.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    outputs.upToDateWhen {false}
    useJUnitPlatform()
    testLogging {
        afterTest { desc, result ->
            logger.quiet "${desc.className} > ${desc.name}: ${result.resultType}"
        }
        // Taken from https://stackoverflow.com/a/36130467
        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
